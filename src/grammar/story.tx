Program:
    'adventure' name=ID
    (hitRanges *= HitRange)
    (variables *= Variable)
    (weapons *= Weapon)
    (treasures *= Treasure)
    (globalRules *= GlobalRule)
    (rooms *= Room)
;

Variable:
    Strength | Gold | Luck | BossStrength
;

Strength:
    'strength' name=ID '=' value=INT
;

BossStrength:
    'boss_strength' name=ID '=' value=INT
;

Gold:
    'gold' name=ID '=' value=INT
;

Luck:
    'luck' name=ID '=' value=INT
;

Weapon:
    'weapon' name=ID 'value' value=INT 'hit_points' hp=INT
;

Treasure:
    'treasure' name=ID 'weight' weight=INT
;

HitRange:
    'define' name=ID '=' '[' from=INT ',' to=INT ']'
;

Fight:
    'fight' description=STRING hr=[HitRange] (condition=Condition)? 'win' winRoom=[Room] 'loose' looseRoom=[Room]
;

Room:
    'room' name=ID '{'
        'imagePath' imagePath=STRING
        'header' header=STRING
        'body' body=STRING
        ((options *= Option) | fight=Fight)
    '}'
;

Option:
    'option' text=STRING (condition=Condition)? (action=Action)? 'goto' target=[Room] ';'
;

Condition:
    '[' varName=[Variable] op=Operator val=INT ']'
;

Action:
    Buy | Take
;

Buy:
    ('buy' item=[Weapon])?  // TODO: from engine take the gold
;

Take:
    ('take' item=[Treasure])? ('set' (assignments+=Assignment | rules+=[GlobalRule] | randoms+=RandomAssign))*
    // TODO: engine should automatically take off the strength (equals to weight of the treasure)
;

Assignment:
    varName=[Variable] '=' exp=Expression
;

Expression:
    left=Term (op=AddOp right=Term)*
;

Term:
    left=Factor (op=MulOp right=Factor)*
;

Factor:
    value=INT
  | var=[Variable]
  | random=Random
  | '(' Expression ')'
;

AddOp:
    '+' | '-'
;

MulOp:
    '*' | '/'
;

Operator:
    '>' | '<' | '==' | '>=' | '<='
;

Random:
    'random(' from=INT ',' to=INT ')'
;

RandomAssign:
    '$' name=ID '=' Random
;

GlobalRule:
    'rule' name=ID '{'
    assignments+=Assignment 
    '}'
;

Comment:
    /\/\/.*$/ | /\/\*[\s\S]*?\*\//
;